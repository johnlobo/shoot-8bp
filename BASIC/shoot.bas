10 GOSUB 10000 'Inicializacin

1' BLUCLE PRINCIPAL 
100 ' lee el teclado y posiciona al personaje
110 c=c+1
120 |STARS, 0, 7, 1, 1, 0
130 IF INKEY(27) THEN GOTO 150 
140 x=x+1:GOTO 170
150 IF INKEY(34) THEN GOTO 170
160 x=x-1
170 IF INKEY(67) THEN GOTO 190
180 y=y-2:GOTO 210 
190 IF INKEY(69) THEN GOTO 210 
200 y=y+2
1' DISPAROS
210 IF c-last<8 THEN 340
220 IF INKEY(47) THEN 340
230 sp=23+sp MOD 8: last=c
240 |SETUPSP,sp,9,17
250 |SETUPSP,sp,0,32+8+1
260 |SETUPSP,sp,5,-6,0
270 |LOCATESP,sp,y-4,x+3
280 SOUND 1,10,10,7,0,1
1' CREACION DE ENEMIGOS
340 IF c AND 63 THEN 500
350 ex=10+RND*50:
360 FOR i=1 TO 4
370 e=e+1: IF e=22 THEN e=10
1'380 |SETUPSP,e,9,26
380 |SETUPSP,e,7,10:|SETUPSP,e,7,32
390 |SETUPSP,e,0,128+8+4+2+1
400 |SETUPSP,e,15,1
410 |LOCATESP,e,-80,ex:|ROUTESP,e,i*6
420 NEXT
1' DRAW SPRITES
500 |LOCATESP,1,y,x
510 |AUTOALL,1:|PRINTSPALL,1,0:|COLSPALL
1' COLISIONES
520 IF cor<32 THEN GOSUB 600:'rutina colision
530 GOTO 110
600 ' BORDER 7: CALL &BD19:BORDER 0
610 IF cor=1 THEN 2000 ELSE SOUND 1,2000,10,7,0,0,15:score=score+10:GOSUB 10300 'UPDATE SCORE
620 |SETUPSP,cor,7,1:|SETUPSP,cor,0,4+1
630 |SETUPSP,cod,7,3:|SETUPSP,cod,0,4+1:BORDER 7: CALL &BD19:BORDER 0 
640 RETURN
1' FINAL
2000 |SETUPSP,1,7,3
2005 |SETUPSP,1,0,4+1
2010 FOR I = 1 TO 32
2020 |AUTOALL,1:|PRINTSPALL,1,0
2030 NEXT
2040 b$=INKEY$:IF b$<>"" THEN 2040
2050 CALL &BC02:'restaura paleta por defecto por si acaso
2060 END
1' INICIALIZACION
10000 MODE 0: DEFINT A-Z: CALL &6B78:' install RSX
10020 GOSUB 10500 'Carga la paleta
10025 level=1
10030 score=0:GOSUB 10300 'UPDATE SCORE
10040 hiscore=0:c$="HI-SCORE:"+STR$(hiscore):|PRINTAT,0,0,60-14,@c$
10050 GOSUB 10100:' DRAW SCREEN
10070 RETURN
1' DRAW SCREEN
1' SHOW FLAG
10100 |SETUPSP,31,9,25
10110 |LOCATESP,31,194,0
10120 |PRINTSP,31
1' SHOW HEARTS
10130 |SETUPSP,31,9,23
10140 FOR i = 0 TO 2
10150 |LOCATESP,31,194,69+(4*i)
10160 |PRINTSP,31
10170 NEXT
1' MAIN CHARACTER
10180 |SETUPSP,1,9,16
10190 |SETUPSP,1,0,1+8+32
10195 |SETLIMITS,0,80,5,194
10200 x=40:y=177:e=11:' coordenadas del personaje
10210 |LOCATESP,1,y,x
10220 |PRINTSP,1
1' EXPLOSION
10230 |SETUPSP,9,7,3
10235 |SETUPSP,9,0,1+4
1' COLISIONES
10240 cod=32:cor=32:|COLSPALL,@cor,@cod
10250 RETURN
1' UPDATE SCORE
10300 c$="SCORE:"+STR$(score):|PRINTAT,0,0,0,@c$
10310 RETURN
1' CARGA LA PALETA
10500 INK 0,&0:INK 1,&D:INK 2,&f:INK 3,&2 
10510 INK 4,&6:INK 5,&E:INK 6,&B:INK 7,&14
10520 INK 8,&9:INK 9,&C:INK 10,&12:INK 11,&3 
10530 INK 12,&6:INK 13,&F:INK 14,&18:INK 15,&1A
10540 RETURN
1